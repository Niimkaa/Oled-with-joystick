#include <U8glib.h>

U8GLIB_SH1106_128X64 u8g(U8G_I2C_OPT_NONE);

#define RIGHT 0
#define LEFT  1
#define UP    2
#define DOWN  3

#define pinX  A0
#define pinY  A1

int valueX = 0;
int valueY = 0;
unsigned char keyValue = 0;

const uint8_t block[] PROGMEM = {
  0xf0, //B1111000
  0xb0, //B1011000
  0xd0, //B1101000
  0xf0, //B1111000
};

uint8_t snake_head_x = 4;
uint8_t snake_head_y = 4;
uint8_t x[100];
uint8_t y[100];
uint8_t snake_len = 2;
uint8_t snake_dir = RIGHT;

uint8_t food_x;
uint8_t food_y;
bool food_eaten = true;

bool game_over = false;
int score = 0;
int level = 1;
int snake_speed = 150;
int i;

void keyScan(void)
{
  static unsigned char keyUp = 1;

  valueX = analogRead(pinX);
  valueY = analogRead(pinY);

  if (keyUp && ((valueX <= 10) || (valueX >= 1010) || (valueY <= 10) || (valueY >= 1010)))
  {
    delay(10);
    keyUp = 0;
    if (valueX <= 10)
    {
      if (snake_dir != UP)
      {
        snake_dir = DOWN;
      }
    }
    else if (valueX >= 1010)
    {
      if (snake_dir != DOWN)
      {
        snake_dir = UP;
      }
    }
    else if (valueY <= 10)
    {
      if (snake_dir != RIGHT)
      {
        snake_dir = LEFT;
      }
    }
    else if (valueY >= 1010)
    {
      if (snake_dir != LEFT)
      {
        snake_dir = RIGHT;
      }
    }
  } else if ((valueX > 10) && (valueX < 1010) && (valueY > 10) && (valueY < 1010)) keyUp = 1;
  return 0;
}

void draw_snake(int x, int y)
{
  u8g.drawBitmap(x, y, 1, 4, block);
}

void show_score(int x, int y, int data)
{
  u8g.setFont(u8g_font_6x10);
  u8g.drawStr(x, y, String(data).c_str());
}

void screen(void)
{
  u8g.firstPage();
  do {
    u8g.drawFrame(0, 1, 102, 62);
    u8g.drawFrame(0, 0, 102, 64);
    u8g.drawStr(104, 12, "LEVE");
    u8g.drawStr(104, 40, "SCOR");

    show_score(110, 25, level);
    show_score(110, 53, score);

    for (i = 0; i < snake_len; i++)
    {
      draw_snake(x[i], y[i]);
    }

    draw_snake(food_x, food_y);

  } while (u8g.nextPage());
}

void draw_food(void)
{
  int food_out = 0;

  if (food_eaten)
  {
    while (food_out == 0)
    {
      food_out = 1;

      food_x = (uint8_t)(random(4, 100) / 4) * 4;
      food_y = (uint8_t)(random(4, 60) / 4) * 4;

      for (int i = snake_len - 1; i > 0; i--)
      {
        if (food_x == x[i] && food_y == y[i])
        {
          food_out = 0;
        }
      }
    }
  }

  food_eaten = false;
}

void snake_move(void)
{
  switch (snake_dir) {
    case RIGHT:
      snake_head_x += 4;
      break;
    case UP:
      snake_head_y -= 4;
      break;
    case LEFT:
      snake_head_x -= 4;
      break;
    case DOWN:
      snake_head_y += 4;
      break;
  }

  if ((snake_head_x == food_x) && (snake_head_y == food_y))
  {
    food_eaten = true;
    snake_len++;
    score++;
    level = score / 5 + 1;
    snake_speed -= level;
  }

  for (i = snake_len - 1; i > 0; i--)
  {
    x[i] = x[i - 1];
    y[i] = y[i - 1];
  }
  x[0] = snake_head_x;
  y[0] = snake_head_y;

  check_snake_die();
}

void draw_game_over()
{
  u8g.firstPage();
  do {
    u8g.setFont(u8g_font_10x20);
    u8g.drawStr(10, 20, "GAME OVER");
    u8g.setFont(u8g_font_6x10);
    u8g.drawStr(30, 35, "LEVE:");
    u8g.drawStr(30, 55, "SCOR:");

    show_score(80, 35, level);
    show_score(80, 55, score);

  } while (u8g.nextPage());
}

void check_snake_die(void)
{
  if (snake_head_x < 4 || snake_head_x > 96 || snake_head_y < 1 || snake_head_y > 56)
  {
    game_over = true;
  }

  if (snake_len > 4)
  {
    for (int i = 1; i < snake_len; i++)
    {
      if (snake_head_x == x[i] && snake_head_y == y[i])
      {
        game_over = true;
      }
    }
  }
}

void setup()
{
  u8g.begin();
  u8g.setFont(u8g_font_unifont);
  randomSeed(analogRead(3));
}

void loop()
{
  if (game_over)
  {
    draw_game_over();
  } else {
    keyScan();
    snake_move();
    draw_food();
    screen();
  }
  delay(snake_speed);
}
